buildscript {
    repositories {
        google()
    }
    dependencies {
        classpath "com.google.firebase:firebase-appdistribution-gradle:$versions.firebase.appdistribution"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.appdistribution'

apply from: '../dependencies-unit-test.gradle'
apply from: '../dependencies-android-test.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.sapuglha.tictactoe"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()

        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
    }

    signingConfigs {
        prod {
            storeFile file("$projectDir/../keystores/prod.keystore")
            keyAlias "prod"
            keyPassword "${System.env.PROD_KEYSTORE_PASSWORD}"
            storePassword "${System.env.PROD_KEYSTORE_PASSWORD}"
        }

        dev {
            storeFile file("$projectDir/../keystores/dev.keystore")
            keyAlias "dev"
            keyPassword "${System.env.DEV_KEYSTORE_PASSWORD}"
            storePassword "${System.env.DEV_KEYSTORE_PASSWORD}"
        }

        debug {
            storeFile file("$projectDir/../keystores/debug.keystore")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false

            // Debug builds will use this keystore, release builds will use the key from each flavor
            signingConfig signingConfigs.debug

            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {

        all {
            buildConfigField "String", "URL", "\"https://www.google.com\""
        }

        // Used for accessing test environment
        dev {
            dimension "default"
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.dev

            buildConfigField "String", "API_VERSION", "\"dev\""

            firebaseAppDistribution {
                apkPath = "/app/build/outputs/apk/dev/release/app-dev-release.apk"
                serviceCredentialsFile = "keystores/serviceAccountFirebase.json"
                groups = "internal"
            }
        }

        // Build that combined with release type will be pushed to the store
        prod {
            dimension "default"
            signingConfig signingConfigs.prod

            buildConfigField "String", "API_VERSION", "\"v1\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures{
        dataBinding = true
        viewBinding = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.kotlinCoroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.kotlinCoroutines"

    // Android support libs
    implementation "androidx.gridlayout:gridlayout:$versions.androidx.gridlayout"
    implementation "androidx.constraintlayout:constraintlayout:$versions.androidx.constraintLayout"

    // Dagger
    implementation "com.google.dagger:dagger:$versions.dagger"
    implementation "com.google.dagger:dagger-android:$versions.dagger"
    implementation "com.google.dagger:dagger-android-support:$versions.dagger"
    kapt "com.google.dagger:dagger-android-processor:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"

    implementation "com.jakewharton.timber:timber:$versions.timber"

    implementation "com.google.firebase:firebase-analytics:$versions.firebase.analytics"
    implementation "com.google.firebase:firebase-crashlytics:$versions.firebase.crashlytics"
}
